{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixpkgs-unstable";
    crane.url = "github:ipetkov/crane";
    flake-compat.url = "github:edolstra/flake-compat";
    flake-parts = {
      url = "github:hercules-ci/flake-parts";
      inputs.nixpkgs-lib.follows = "nixpkgs";
    };
    treefmt-nix = {
      url = "github:numtide/treefmt-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    inputs:
    inputs.flake-parts.lib.mkFlake { inherit inputs; } {
      systems = [
        "x86_64-linux"
        "aarch64-linux"
        "aarch64-darwin"
      ];

      imports = [
        inputs.treefmt-nix.flakeModule
      ];

      perSystem =
        {
          pkgs,
          lib,
          system,
          ...
        }:
        let
          rust = pkgs.rust-bin.fromRustupToolchainFile ./rust-toolchain.toml;
          craneLib = (inputs.crane.mkLib pkgs).overrideToolchain rust;
          overlays = [ inputs.rust-overlay.overlays.default ];
          src = lib.cleanSource ./.;

          buildInputs =
            lib.optionals pkgs.stdenv.hostPlatform.isLinux [
              pkgs.pkg-config
              pkgs.udev
              pkgs.alsa-lib
              pkgs.vulkan-loader
              pkgs.libX11
              pkgs.libXcursor
              pkgs.libXi
              pkgs.libXrandr
              pkgs.libxkbcommon
              pkgs.wayland
            ]
            ++ [
              pkgs.llvmPackages.libclang.lib
            ];
          nativeBuildInputs = [
            pkgs.pkg-config # pkg-config
            pkgs.makeWrapper # For the Nix packaging
            pkgs.nil # Nix LSP
            rust # Rust toolchain
            pkgs.cargo-llvm-cov
            pkgs.nushell # Script runner
            pkgs.cachix # cachix CLI
          ];
          cargoArtifacts = craneLib.buildDepsOnly {
            inherit src buildInputs nativeBuildInputs;

            LIBCLANG_PATH = lib.makeLibraryPath buildInputs;
            LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;
          };
          web-phone = craneLib.buildPackage {
            inherit
              src
              cargoArtifacts
              buildInputs
              nativeBuildInputs
              ;
            strictDeps = true;
            doCheck = true;

            LIBCLANG_PATH = lib.makeLibraryPath buildInputs;
            LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;

            meta = {
              licenses = [ lib.licenses.mit ];
              mainProgram = "spr";
            };
          };
          cargo-clippy = craneLib.cargoClippy {
            inherit
              src
              cargoArtifacts
              buildInputs
              nativeBuildInputs
              ;
            cargoClippyExtraArgs = "--verbose -- --deny warnings";

            LIBCLANG_PATH = lib.makeLibraryPath buildInputs;
            LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;
          };
          cargo-doc = craneLib.cargoDoc {
            inherit
              src
              cargoArtifacts
              buildInputs
              nativeBuildInputs
              ;

            LIBCLANG_PATH = lib.makeLibraryPath buildInputs;
            LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;
          };
          llvm-cov = craneLib.cargoLlvmCov {
            inherit
              src
              cargoArtifacts
              buildInputs
              nativeBuildInputs
              ;

            LIBCLANG_PATH = lib.makeLibraryPath buildInputs;
            LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;
            cargoLlvmCovExtraArgs = "test --html --output-dir $out";
          };
        in
        {
          _module.args.pkgs = import inputs.nixpkgs {
            inherit system overlays;
          };

          treefmt = {
            projectRootFile = "flake.nix";

            # Nix
            programs.nixfmt.enable = true;

            # Rust
            programs.rustfmt.enable = true;
            settings.formatter.rustfmt.command = "${rust}/bin/rustfmt";

            # TOML
            programs.taplo.enable = true;

            # GitHub Actions
            programs.actionlint.enable = true;

            # Markdown
            programs.mdformat.enable = true;

            # ShellScript
            programs.shellcheck.enable = true;
            programs.shfmt.enable = true;
          };

          packages = {
            inherit web-phone llvm-cov;
            default = web-phone;
            doc = cargo-doc;
          };

          checks = {
            inherit cargo-clippy;
          };

          devShells.default = pkgs.mkShell {
            inherit buildInputs nativeBuildInputs;

            LIBCLANG_PATH = lib.makeLibraryPath buildInputs;
            LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;

            shellHook = ''
              export PS1="\n[nix-shell:\w]$ "
            '';
          };
        };
    };
}
